apply plugin: 'com.android.application'

android {
    namespace 'org.strongswan.android'

    defaultConfig {
        applicationId "org.strongswan.android"
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 33

        versionCode 82
        versionName "2.5.1"

        externalNativeBuild {
            ndkBuild {
                arguments '-j' + Runtime.runtime.availableProcessors()
            }
        }
    }

    ndkVersion "26.1.10909125"

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        compileTask ->
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-process:2.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.10.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-core:5.8.0'
}
